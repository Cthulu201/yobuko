# Maintainer: Alexandre Pujol <alexandre@pujol.io>

pkgname='pass-import-git'
_pkgname="${pkgname%-git}"
pkgver=v3.4.r28.gf0416de
pkgrel=1
pkgdesc='A pass extension for importing data from most existing password managers.'
arch=('any')
url='https://github.com/roddhjav/pass-import'
license=('GPL3')
depends=( 'pass' 'python' 'python-pyaml' 'python-zxcvbn' 'python-requests')
makedepends=('python-setuptools' 'git' 'pandoc')
optdepends=('python-defusedxml: xml based importers'
            'python-pykeepass: direct keepass database import'
            'python-secretstorage: gnome-keyring import'
            'python-cryptography: password encrypted otp import'
            'python-magic: Detection of file decryption')
replaces=("pass-import")
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}" || exit
  ( set -o pipefail
    git describe --long --abbrev=7 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd "${srcdir}/${pkgname}" || exit
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}/share/man/man1" || exit
  pandoc -o pass-import.1 pass-import.md
  pandoc -o pimport.1 pimport.md

  cd "${srcdir}/${pkgname}" || exit
  python setup.py install --root="${pkgdir}" -O1 --skip-build
}
