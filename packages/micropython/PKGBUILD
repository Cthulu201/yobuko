# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgbase="micropython"
pkgname=("micropython" "micropython-lib")
pkgver=1.17
_libver=1.9.3
pkgrel=1
pkgdesc="Python3 for microcontrollers, with stdlib.  (unix version)"
arch=('x86_64')
url="http://micropython.org/"
license=('MIT')
groups=('yobuko')
depends=('readline' 'libffi' 'mbedtls')
makedepends=('python' 'python-setuptools' 'git')
options=('!emptydirs')
source=("https://micropython.org/resources/source/micropython-${pkgver}.tar.xz"
        "mlib-${_libver}.tgz::https://github.com/micropython/micropython-lib/archive/v${_libver}.tar.gz")
sha512sums=('8074214040e35ae1ca99721ec442f4cef70fa03380b7662016c55cb3acff9178fc54140c0d818c9581389b4daaade142ecf75b65a2919c666a50255423a39a54'
            '6b4ae58400bd09b8958195540902c463115f129486cd653b897459431451c737ce7e04371be12993c5ceb00f4bfe0d970c1094c4de0827be847e3a0de7f17c78')

# todo:
# fix 'imported as namespace package' warnings from stdlib

prepare() {
  # fix makefile
  cd "${srcdir}/micropython-${pkgver}/ports/unix"
  sed -i 's|/local||' Makefile
  sed -i 's|git clean -d -x -f||' Makefile

  # use mbedtls
  sed -i 's/MICROPY_PY_USSL = 1/MICROPY_PY_USSL = 0/' mpconfigport.mk
  sed -i 's/MICROPY_SSL_AXTLS = 1/MICROPY_SSL_AXTLS = 0/' mpconfigport.mk
  sed -i 's/MICROPY_SSL_MBEDTLS = 0/MICROPY_SSL_MBEDTLS = 1/' mpconfigport.mk
}

build() {
  cd "${srcdir}/micropython-${pkgver}/mpy-cross"
  make
  cd "${srcdir}/micropython-${pkgver}/ports/unix"
  #make libffi
  #make V=1 deplibs
  make
  cd "${srcdir}/micropython-lib-${_libver}"
  make
}

check() {
  cd "${srcdir}/micropython-${pkgver}/ports/unix"
  # some tests appear non-deterministic
  #make test
}

package_micropython() {
  cd "${srcdir}/micropython-${pkgver}/ports/unix"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/micropython-${pkgver}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_micropython-lib() {
  cd "${srcdir}/micropython-lib-${_libver}"
  for _d in $(find -mindepth 1 -maxdepth 1 -type d); do
    # todo, figure out what provides this
    # maybe importlib.metadata in python 3.8?
    if grep -qr 'import metadata' ./$_d/*; then
      error "Skipping $_d (bad import)"
      continue
    fi
    if [[ ! -f $_d/setup.py ]]; then
      error "Skipping $_d (no setup)"
      continue
    fi
    # todo: fixing this probably involves overriding PYTHONPATH
    if [[ "$_d" == "./types" ]]; then
      error "Skipping $_d (unresolved name collision)"
      continue
    fi
    msg "Installing $_d"
    cd $_d
    python3 setup.py install --prefix=/usr --root="${pkgdir}" --install-lib='/usr/lib/micropython'
    cd ..
  done

  # micropython doesn't support pyc
  find "${pkgdir}" -name '*.pyc' -delete

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
