# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname=brave-browser
pkgver=1.63.169
pkgrel=1
pkgdesc='A fast, private and secure web browser for PC, Mac and mobile'
arch=(x86_64)
url=https://brave.com
license=(MPL2 BSD custom:chromium)
groups=('yobuko')
depends=('alsa-lib' 'gtk3' 'libxss' 'nss' 'ttf-font')
makedepends=('unzip')
optdepends=('cups:             Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify:        Native notification support')
replaces=("${pkgname%-browser}-bin")
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/brave/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}-linux-amd64.zip")
noextract=("${pkgname}-${pkgver}.zip")
sha512sums=('6834fd2fb25755ff2702fd805bb62c34f08c95c01986ec331b20087e3e6ae70b0bc0cd027fba83e6eaa6115e987921cdfaa7d52365331d861d3c0cff2be9b591')

prepare() {
  unzip "${pkgname}-${pkgver}.zip" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit

  install -dm0755 "${pkgdir}/usr/bin"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for size in 16 24 32 48 64 128 256; do
    install -Dm644 "product_logo_${size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/brave-browser.png"
  done
  rm product_logo*

  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
