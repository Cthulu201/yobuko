# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname=brave-browser
pkgver=1.61.116
pkgrel=1
pkgdesc='A fast, private and secure web browser for PC, Mac and mobile'
arch=(x86_64)
url=https://brave.com
license=(MPL2 BSD custom:chromium)
groups=('yobuko')
depends=('alsa-lib' 'gtk3' 'libxss' 'nss' 'ttf-font')
makedepends=('unzip')
optdepends=('cups:             Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify:        Native notification support')
replaces=("${pkgname%-browser}-bin")
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/brave/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}-linux-amd64.zip")
noextract=("${pkgname}-${pkgver}.zip")
sha512sums=('10908a2f320b619e389408d2f24640275443920e4a0fb17fd3f71926cd74715ed5d56f394a518f322926fbcd5d699464cf4069fefeb5bb349c0ee3e4a5a4c43d')

prepare() {
  unzip "${pkgname}-${pkgver}.zip" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit

  install -dm0755 "${pkgdir}/usr/bin"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for size in 16 24 32 48 64 128 256; do
    install -Dm644 "product_logo_${size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/brave-browser.png"
  done
  rm product_logo*

  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
