# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname=brave-browser
pkgver=1.67.123
pkgrel=1
pkgdesc='A fast, private and secure web browser for PC, Mac and mobile'
arch=(x86_64)
url=https://brave.com
license=(MPL2 BSD custom:chromium)
groups=('yobuko')
depends=('alsa-lib' 'gtk3' 'libxss' 'nss' 'ttf-font')
makedepends=('unzip')
optdepends=('cups:             Printer support'
	'libgnome-keyring: Enable GNOME keyring support'
	'libnotify:        Native notification support')
replaces=("${pkgname%-browser}-bin")
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/brave/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}-linux-amd64.zip"
	"${pkgname}.desktop")
noextract=("${pkgname}-${pkgver}.zip")
sha512sums=('94805913dad46bb840e860b7d646fbb52b6eebe4660832d6e4bc38cf12895765b8cb5f9817ee5e143e08433a6b6a69497ac2fc61efc05380a90b1d215f22275b'
            '184f9ae63d41d199d3ba4ad69f885cefb7d3ccf464a1a72cf4dafadf65dbfd12e986250641d9364b054367adb79cd695c0227f18c4dda45258214021d5e60c56')

prepare() {
	unzip "${pkgname}-${pkgver}.zip" -d "${pkgname}-${pkgver}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}" || exit

	install -dm0755 "${pkgdir}/usr/bin"
	install -dm0755 "${pkgdir}/opt/${pkgname}"
	install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm0644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

	for size in 16 24 32 48 64 128 256; do
		install -Dm644 "product_logo_${size}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/brave-browser.png"
	done
	rm product_logo*

	cp -a ./* "${pkgdir}/opt/${pkgname}/"
	chmod 4755 "${pkgdir}/opt/${pkgname}/chrome-sandbox"
	ln -s "/opt/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
