# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname=brave-browser
pkgver=1.60.110
pkgrel=1
pkgdesc='A fast, private and secure web browser for PC, Mac and mobile'
arch=(x86_64)
url=https://brave.com
license=(MPL2 BSD custom:chromium)
groups=('yobuko')
depends=('alsa-lib' 'gtk3' 'libxss' 'nss' 'ttf-font')
makedepends=('unzip')
optdepends=('cups:             Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify:        Native notification support')
replaces=("${pkgname%-browser}-bin")
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/brave/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}-linux-amd64.zip")
noextract=("${pkgname}-${pkgver}.zip")
sha512sums=('1672414dff5fc8e0412752e6dd64649ba20cc10f6811dd27ec4e799a3d5724f57271cc67b1dbae52889d437493de11ef674e8f2cef5acef2cd32b5b25de5e003')

prepare() {
  unzip "${pkgname}-${pkgver}.zip" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit

  install -dm0755 "${pkgdir}/usr/bin"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
