# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname=powershell-bin
pkgver=7.3.4
pkgrel=1
pkgdesc='A cross-platform automation and configuration tool/framework (binary package)'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://github.com/Powershell/Powershell'
license=('MIT')
groups=('yobuko')
depends=('krb5' 'gcc-libs' 'glibc' 'lttng-ust' 'zlib' 'icu')
provides=('powershell')
conflicts=('powershell')
options=(staticlibs !strip)
install=powershell.install
_artifact="${pkgname}-${pkgver}-${pkgrel}.tar.gz"
source=("${_artifact}::${url}/releases/download/v${pkgver}/${pkgname//-bin/""}-${pkgver}-linux-x64.tar.gz")
noextract=("$_artifact")
sha512sums=('19529e64379886f4017b26cf72112751f73aa22a8a2405b666dd39a2595f42666c102cd70ef3ad9a85e76cdfd18ab6dcc0deb624a20a07951baa8ce4b14e47e0')

prepare() {
  cd "${srcdir}" || exit
  mkdir -p "${pkgname}-${pkgver}-${pkgrel}"
  tar -xf "${_artifact}" -C "${pkgname}-${pkgver}-${pkgrel}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-${pkgrel}" || exit

  for path in $(find "$(pwd)" -type f); do
    path_rel=$(realpath --relative-to="$(pwd)" "$path")
    install -Dm644 "${path_rel}" "${pkgdir}/opt/microsoft/powershell/7/${path_rel}"
  done

  chmod 755 "${pkgdir}/opt/microsoft/powershell/7/pwsh"
  mkdir -p "${pkgdir}/usr/bin/"
  ln -s "/opt/microsoft/powershell/7/pwsh" "${pkgdir}/usr/bin/pwsh"

  install -Dm0644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/powershell-bin/LICENSE"
}
