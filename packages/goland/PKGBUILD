# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="GoLand"
pkgname="goland"
pkgver=2023.1.3
pkgrel=1
pkgdesc="Capable and Ergonomic Go IDE"
arch=('x86_64')
url='https://www.jetbrains.com/go/'
license=(custom)
groups=('yobuko')
depends=('glib2')
options=('!strip')
source=("https://download.jetbrains.com/go/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b39da960001e647354f17a59e5f34cb58893378a48cb08c15b6567bdef2c38a7cc0ea4e5aa36bfeab2ec7da6900b3b3f54b272f986a0d14ee35b566d8c374101')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
