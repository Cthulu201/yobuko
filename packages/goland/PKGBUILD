# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="GoLand"
pkgname="goland"
pkgver=2023.1.2
pkgrel=1
pkgdesc="Capable and Ergonomic Go IDE"
arch=('x86_64')
url='https://www.jetbrains.com/go/'
license=(custom)
groups=('yobuko')
depends=('glib2')
options=('!strip')
source=("https://download.jetbrains.com/go/${pkgname}-${pkgver}.tar.gz")
sha512sums=('de3a19de0d2f1b77d3c05caaf01a0d1918bd38e03ccb288cc68bfd36f6bb0a447dd137a04ca482cf4f96683eec3223cfa313944359a5055d8f38ca90f51efa1e')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
