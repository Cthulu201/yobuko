# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="GoLand"
pkgname="goland"
pkgver=2022.1.3
pkgrel=1
pkgdesc="Capable and Ergonomic Go IDE"
arch=('x86_64')
url='https://www.jetbrains.com/go/'
license=(custom)
groups=('yobuko')
depends=('glib2')
options=('!strip')
source=("https://download.jetbrains.com/go/${pkgname}-${pkgver}.tar.gz")
sha512sums=('22028f800a07217b4e0535c2fb4e64ed0b1e37f2f3eefbe4d8e6042c32d1bbcf8b1f29d2cee4b288160513661d28e31711dd94ac400de600039d7e3d9e968931')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j-native/linux/"
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # Install directories
  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm 755 "${pkgdir}/opt/${pkgname}"
  install -dm 755 "${pkgdir}/usr/bin/"

  # Install licenses
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
