# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="GoLand"
pkgname="goland"
pkgver=2022.3.4
pkgrel=1
pkgdesc="Capable and Ergonomic Go IDE"
arch=('x86_64')
url='https://www.jetbrains.com/go/'
license=(custom)
groups=('yobuko')
depends=('glib2')
options=('!strip')
source=("https://download.jetbrains.com/go/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b2439f343a2e7cfa6907e6b2753232ecb7c72560c9483167ba5b0e974e55f53ee69c9d81163e51b146c28deb811822a5f169bb8660cc02f292153bc121e1bf5e')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
