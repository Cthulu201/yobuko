# Maintainer: Cthulu201 <cthulu201@duck.com>
# Contributor: Guilhelm Savin <aur@gsav.in>
# Contributor: Jake <aur@ja-ke.tech>

# Upstream URL: https://github.com/platformio/platformio

pkgname=('platformio' 'platformio-udev-rules')
pkgver=6.1.13
pkgrel=1
pkgdesc="A cross-platform code builder and library manager"
arch=('any')
url="https://github.com/platformio/platformio-core"
license=('Apache')
groups=('yobuko')
depends=('python-setuptools'
         'python-bottle'
         'python-click'
         'python-colorama'
         'python-pyserial>=3.4' #https://github.com/platformio/platformio-core/commit/a37eb9868f3b20e982d0c3cd1a742fcb8ab60efc
         'python-requests'
         'python-semantic-version'
         'python-tabulate'
         'python-pyelftools'
         'python-marshmallow'
         'python-zeroconf'
         'python-aiofiles'
         'python-ajsonrpc'
         'python-starlette'
         'python-wsproto'
         'uvicorn')
optdepends=('python-click-completion: for shell completions'
            'python-shellingham:       for shell completions')
conflicts=('platformio-git')
source=("${pkgname}-${pkgver}::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('cd71720c5a293299bc357d9eee82344bea61f44c68bc1b0d287a14c3531cd1f0d8d1d265e7c4a670935b69f93d9f360216cfc613c0c9c3f653415df8ae8f8069')

package_platformio() {
  cd "${srcdir}/platformio-core-${pkgver}" || exit
  python setup.py install --root="${pkgdir}/" -O1
}

package_platformio-udev-rules() {
  depends=('udev')
  pkgdesc="udev rules for PlatformIO supported boards/devices"
  url="https://docs.platformio.org/en/latest/faq.html#platformio-udev-rules"

  cd "${srcdir}/platformio-core-${pkgver}" || exit
  install -m644 -Dt "${pkgdir}/usr/lib/udev/rules.d" "platformio/assets/system/99-platformio-udev.rules"
}
