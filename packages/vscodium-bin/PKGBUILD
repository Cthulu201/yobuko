# Maintainer: Cthulu201 <cthulu201@duck.com>
# Contributor: Carson Rueter <roachh at proton mail dot com>
# Contributor: Icelk <main at icelk.dev>
# Contributor: Cameron Katri <katri.cameron@gmail.com>
# Contributor: Plague-doctor <plague <at>> privacyrequired <<dot>> com >
# Contributor: me at oguzkaganeren dot com dot tr
# Contributor: Rowisi < nomail <at> private <dot> com >

pkgname="vscodium-bin"
pkgver=1.80.0.23188
pkgrel=1
pkgdesc="Binary releases of VS Code without MS branding/telemetry/licensing."
arch=('x86_64')
url="https://github.com/VSCodium/${pkgname//-bin/""}"
license=('MIT')
groups=('yobuko')
depends=('fontconfig' 'libxtst' 'gtk3' 'python' 'cairo' 'alsa-lib' 'nss'
         'gcc-libs' 'libnotify' 'libxss' 'glibc>=2.28-4')
optdepends=('gvfs:            For move to trash functionality'
           'libdbusmenu-glib: For KDE global menu')
provides=('codium' "${pkgname//-bin/""}")
conflicts=("${pkgname//-bin/""}" "${pkgname//-bin/""}-git")
source=("https://github.com/VSCodium/${pkgname//-bin/""}/releases/download/${pkgver}/VSCodium-linux-x64-${pkgver}.tar.gz")
sha512sums=('f89bc38d4a165233b6a1fced6742937258c06e9abeb1759018dc1c4f6f0f794ae8e89acd9cd83519a6db9d5dc0174fc17ea0443ab8187116c328980e555b6232')

shopt -s extglob

package() {
  cd "${srcdir}" || exit
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin"
  install -dm0755 "${pkgdir}/usr/share"/{applications,pixmaps}
  cp -r "${srcdir}"/!(vscodium-bin?(-uri-handler).desktop|"${pkgname//-bin/""}"-linux-x64-"${pkgver}".tar.gz) "${pkgdir}/opt/${pkgname}"
  ln -s "/opt/${pkgname}/bin/codium" "${pkgdir}/usr/bin/codium"
  ln -s "/opt/${pkgname}/bin/codium" "${pkgdir}/usr/bin/vscodium"
  install -Dm0644 "resources/app/resources/linux/code.png" "${pkgdir}/usr/share/pixmaps/${pkgname//-bin/""}.png"

  # Symlink shell completions
  install -dm0755 "${pkgdir}/usr/share/zsh/site-functions"
  install -dm0755 "${pkgdir}/usr/share/bash-completion/completions"
  ln -s "/opt/${pkgname}/resources/completions/zsh/_codium" "${pkgdir}/usr/share/zsh/site-functions"
  ln -s "/opt/${pkgname}/resources/completions/bash/codium" "${pkgdir}/usr/share/bash-completion/completions"
}
