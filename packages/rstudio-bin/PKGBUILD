# Maintainer: Cthulu201 <cthulu201@duck.com>
# Contributor: Maintainer: Meow

pkgname=rstudio-bin
pkgver=2023.06.2_561
pkgrel=1
pkgdesc="An integrated development environment (IDE) for R (binary from RStudio official repository)"
arch=('x86_64')
url="http://www.rstudio.org/"
license=('GPL')
groups=('yobuko')
depends=('r>=3.3.0' 'hicolor-icon-theme' 'shared-mime-info' 'openssl' 'openssl-1.1'
         'at-spi2-core' 'gtk3' 'alsa-lib' 'libedit' 'postgresql-libs' 'sqlite')
optdepends=('clang: C/C++ and Rcpp support')
provides=("rstudio-desktop")
conflicts=('rstudio-desktop' 'rstudio-desktop-git' 'rstudio-desktop-preview-bin')
options=(!strip)
install="${pkgname}".install
source=("https://download1.rstudio.org/electron/jammy/amd64/rstudio-${pkgver//_/-}-amd64.deb"
        "rstudio-bin.install")
sha512sums=('2d81f9b8b3e8a022b108ca96d4ec0816956529421ea4503194c9077e896274fd36a578d583096956e65fe02ae6479009562dfc62472bfa8bcc433685010ac960'
            '97526989479c289244bad580ce0f1928775dcc2d31dfb18ce1bdccf1bc958398a5f4b1e93f9e2efbca949079f306c416b8e5a45672677a90d7c5b78bac3b8905')

package() {
  shopt -s extglob
  cd "${srcdir}"
  install -d "${pkgdir}/usr/bin"
  bsdtar xf data.tar.xz -C "${pkgdir}"
  ln -s /usr/lib/rstudio/rstudio "${pkgdir}/usr/bin/rstudio"
}
