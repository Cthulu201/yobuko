# Maintainer: Cthulu201 <cthulu201@duck.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: José Miguel Sarasola <jmsaraur@gmail.com>
# Contributor: Như Bảo Trương <28810481+nhubaotruong@users.noreply.github.com>
# Contributor: Andrés Rodríguez <hello@andres.codes>
# Contributor: Jacob Mischka <jacob@mischka.me>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>
# Contributor: Simón Oroño <simonorono@protonmail.com>
# Contributor: now-im <now im 627 @ gmail . com>
# Contributor: Giusy Digital <kurmikon at libero dot it>

# Check for latest release:
# `curl https://brave-browser-downloads.s3.brave.com/latest/release.version`

pkgname=brave-bin
pkgver=1.49.128
pkgrel=1
pkgdesc='A fast, private and secure web browser for PC, Mac and mobile'
arch=(x86_64)
url=https://brave.com
license=(MPL2 BSD custom:chromium)
groups=('yobuko')
depends=('alsa-lib' 'gtk3' 'libxss' 'nss' 'ttf-font')
optdepends=('cups:             Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify:        Native notification support')
provides=("${pkgname%-bin}=${pkgver}" 'brave-browser')
conflicts=("${pkgname%-bin}")
replaces=("${pkgname%-bin}")
options=(!strip)
source=("https://github.com/brave/${pkgname//-bin/""}-browser/releases/download/v${pkgver}/${pkgname//-bin/""}-browser-${pkgver}-linux-amd64.zip"
        "${pkgname}.sh")
noextract=("${pkgname//-bin/""}-browser-${pkgver}-linux-amd64.zip")
sha512sums=('d86670c32127f56915681936f903782139f75a71b14248771ccbb461c4ac2fbf2e53e16ea424d33458425b146ad7be676592e3bb74c5392df29b82716371d1a2'
            '6912f1e0b20f16078e15ce75eca61b29fd5c415c63df9bd69a547d402bf00599fdcbe1c0898b8d2dc865dc1b604f81e2ec21525ecc61a0fcafba0d9f87434540')

prepare() {
  cd "${srcdir}" || exit
  mkdir -p brave
  bsdtar -xf "${pkgname//-bin/""}-browser-${pkgver}-linux-amd64.zip" -C brave
  chmod +x brave/brave
}

package() {
  cd "${srcdir}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/lib"
  cp -a brave "${pkgdir}/usr/lib/${pkgname}"
  
  # allow firejail users to get the suid sandbox working
  chmod 4755 "${pkgdir}/usr/lib/brave-bin/chrome-sandbox"
  
  # Install file(s)
  install -Dm0755 "${pkgname}.sh" "${pkgdir}/usr/bin/brave"
  install -Dm0644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" brave/LICENSE
  pushd "${pkgdir}/usr/" || exit
  for size in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
  	install -Dm0644 "lib/${pkgname}/product_logo_${size/x*/}.png" \
  		"share/icons/hicolor/${size}/apps/brave-desktop.png"
  done
}
