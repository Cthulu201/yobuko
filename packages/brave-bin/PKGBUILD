# Maintainer: Cthulu201 <cthulu201@duck.com>

# Check for latest release:
# `curl https://brave-browser-downloads.s3.brave.com/latest/release.version`

pkgname=brave-bin
pkgver=1.58.131
pkgrel=1
pkgdesc='A fast, private and secure web browser for PC, Mac and mobile'
arch=(x86_64)
url=https://brave.com
license=(MPL2 BSD custom:chromium)
groups=('yobuko')
depends=('alsa-lib' 'gtk3' 'libxss' 'nss' 'ttf-font')
makedepends=('unzip')
optdepends=('cups:             Printer support'
            'libgnome-keyring: Enable GNOME keyring support'
            'libnotify:        Native notification support')
provides=("${pkgname%-bin}=${pkgver}" 'brave-browser')
conflicts=("${pkgname%-bin}")
replaces=("${pkgname%-bin}")
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/brave/${pkgname%-bin}-browser/releases/download/v${pkgver}/${pkgname%-bin}-browser-${pkgver}-linux-amd64.zip")
noextract=("${pkgname}-${pkgver}.zip")
sha512sums=('d8ef47f38123c31efac54c44b66075a231793cb2d327c422b7d1619f612c8c47ade2010c46dc3e0af0b29b1d201bf4bdff90030bc076ba9c1fb96d8bf3e19fe3')

prepare() {
  unzip "${pkgname}-${pkgver}.zip" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit

  install -dm0755 "${pkgdir}/usr/bin"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -Dm0644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/${pkgname%-bin}-browser" "${pkgdir}/usr/bin/${pkgname%-bin}-browser"
}
