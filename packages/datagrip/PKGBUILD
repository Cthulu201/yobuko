# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="DataGrip"
pkgname="datagrip"
pkgver=2023.1.1
pkgrel=1
pkgdesc="A database management environment for developers"
arch=('x86_64')
url="https://www.jetbrains.com/datagrip"
license=(custom)
groups=('yobuko')
depends=(desktop-file-utils gtk-update-icon-cache)
optdepends=('mariadb:    Fast SQL database server derived from MySQL'
            'postgresql: Sophisticated object-relational DBMS'
            'sqlite:     A C library that implements an SQL database engine')
options=(!strip)
source=("https://download.jetbrains.com/datagrip/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1edc3a3d13163c37d44f6deff61a69bafe99d9c4a095f3131fa8f542078b3ae9fc4bc3e7b3ea8b9e7f3efc676a58a5ac5a22cf66837eb6afb676723b7f7087d3')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
