# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname="clion"
pkgver=2022.1.3
pkgrel=1
pkgdesc="A cross-platform IDE for C and C++"
arch=('x86_64')
url="http://www.jetbrains.com/${_pkgname}"
license=('custom')
groups=('yobuko')
depends=('python')
source=("https://download.jetbrains.com/cpp/CLion-${pkgver}.tar.gz")
sha512sums=('5caabc409e9e22e59a26886cb5a5785cc0f2c193e7548a9757d11b4e2b7dad5af7545f3c71b83e3111deb0e0c90507eb5ee6020dcba40e413c0882f2743ac49d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/lib/pty4j-native/linux/"
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Install directories
  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm 755 "${pkgdir}/opt/${pkgname}"
  install -dm 755 "${pkgdir}/usr/bin/"

  # Install licenses
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
