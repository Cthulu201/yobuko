# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname="clion"
pkgver=2021.2.3
pkgrel=3
pkgdesc="A cross-platform IDE for C and C++"
arch=('x86_64')
url="http://www.jetbrains.com/${_pkgname}"
license=('custom')
groups=('yobuko')
depends=('python')
source=("https://download.jetbrains.com/cpp/CLion-${pkgver}.tar.gz")
sha512sums=('ae7a65f5bd746b260a171128f0a93285b70c7f2e0f18bd110c5b75d895993391bd0a0df5a7ab2d331481f911cceba0d1bd49a75f0dd5e50da8beb52e6714cb1c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/lib/pty4j-native/linux/"
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Install directories
  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm 755 "${pkgdir}/opt/${pkgname}"
  install -dm 755 "${pkgdir}/usr/bin/"

  # Install licenses
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
