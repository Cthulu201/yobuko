# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname="clion"
pkgver=2022.3.2
pkgrel=3
pkgdesc="A cross-platform IDE for C and C++"
arch=('x86_64')
url="http://www.jetbrains.com/${_pkgname}"
license=('custom')
groups=('yobuko')
depends=('python')
source=("https://download.jetbrains.com/cpp/CLion-${pkgver}.tar.gz")
sha512sums=('7ca87933323c0e1a4800eb75fbe40d179af987f33de51bca0e6b35264aaffea991712187719b837e07bd11457f8891f9f6038fb10f4e3157170d7818179b7531')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit
  # Install directorie(s)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
