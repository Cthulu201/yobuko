# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="RubyMine"
pkgname="rubymine"
pkgver=2023.1
pkgrel=3
pkgdesc="Ruby and Rails IDE with the full stack of essential developer tools"
arch=('x86_64')
url=https://www.jetbrains.com/ruby/
license=(custom)
groups=('yobuko')
depends=(desktop-file-utils gtk-update-icon-cache)
optdepends=('ruby: Ruby run/debug support')
options=(!strip)
source=(https://download.jetbrains.com/ruby/${_pkgname}-${pkgver}.tar.gz)
sha512sums=('25b1f552e5016cdcb7896db6dadac170fbf484f37b321c88cb7509a046201c3afb58bfc55e6ac40203a4703b2ac0d593637eaee0799c22feca3f7609fbbc43c8')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
