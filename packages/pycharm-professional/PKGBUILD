# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname="pycharm-professional"
pkgver=2023.1.3
pkgrel=1
pkgdesc="Python IDE for Professional Developers. Professional Edition"
arch=('x86_64')
url='https://www.jetbrains.com/pycharm/'
license=('custom')
groups=('yobuko')
depends=('python')
makedepends=('python-setuptools' 'cython')
optdepends=('python2:           For Python 2 development'
            'ipython:           For enhanced interactive Python shell inside Pycharm'
            'openssh:           For deployment and remote connections'
            'python-setuptools: Packages manager for Python, for project interpreter'
            'python-coverage:   For support code coverage measurement'
            'cython:            For performance debugger'
            'docker-machine:    For support docker inside Pycharm'
            'docker-compose:    For support docker inside Pycharm'
            'vagrant:           For support virtualized development environments'
            'python-pytest:     For support testing inside Pycharm'
            'python-tox:        Python environments for testing tool'
            'jupyter-notebook:  For support Jupyter Notebook')
provides=('pycharm-professional')
conflicts=('pycharm' 'pycharm-community-edition')
options=(!strip)
source=("https://download.jetbrains.com/python/${pkgname}-${pkgver}.tar.gz")
sha512sums=('edbb68ec75c0387634c3f96040a73ba7fc855e51eeac452db7a162df13999707f3794fe408cec76903ab036a3e4998726142c0e396a3bf0af9c300dfbb7bb6cb')

build() {
  cd "${srcdir}/${pkgname//-professional/""}-${pkgver}/lib/pty4j/linux/" || exit
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${pkgname//-professional/""}-${pkgver}" || exit
  # Install director(y/ies)
  install -dm0755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -dm0755 "${pkgdir}/usr/bin/"

  # Install license(s)
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable(s)
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${pkgname//-professional/""}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
