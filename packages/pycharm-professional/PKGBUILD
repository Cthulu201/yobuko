# Maintainer: Cthulu201 <cthulu201@duck.com>

_pkgname="pycharm"
pkgname="${_pkgname}-professional"
pkgver=2022.1.3
pkgrel=1
pkgdesc="Python IDE for Professional Developers. Professional Edition"
arch=('x86_64')
url='https://www.jetbrains.com/pycharm/'
license=('custom')
groups=('yobuko')
depends=('python')
makedepends=('python-setuptools' 'cython')
optdepends=('python2:           For Python 2 development'
            'ipython:           For enhanced interactive Python shell inside Pycharm'
            'openssh:           For deployment and remote connections'
            'python-setuptools: Packages manager for Python, for project interpreter'
            'python-coverage:   For support code coverage measurement'
            'cython:            For performance debugger'
            'docker-machine:    For support docker inside Pycharm'
            'docker-compose:    For support docker inside Pycharm'
            'vagrant:           For support virtualized development environments'
            'python-pytest:     For support testing inside Pycharm'
            'python-tox:        Python environments for testing tool'
            'jupyter-notebook:  For support Jupyter Notebook')
provides=('pycharm-professional')
conflicts=('pycharm' 'pycharm-community-edition')
source=("https://download.jetbrains.com/python/${pkgname}-${pkgver}.tar.gz")
sha512sums=('cf32d6a704f9b81206b3b8fbf93f37438d971f434df4ebf1fb59dcf3be2357258fca6829c94a93722b21287064e2be602e1e455b47fde7cabee032fcadcabc30')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/lib/pty4j-native/linux/"
  find . -maxdepth 1 -not -path . -type d -not -name 'x86-64' -exec rm -rf {} \;
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # Install directories
  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -dm 755 "${pkgdir}/opt/${pkgname}"
  install -dm 755 "${pkgdir}/usr/bin/"

  # Install licenses
  mv "license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  rmdir "license"

  # Setup executable
  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/bin/${_pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
