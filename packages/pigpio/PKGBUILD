# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname='pigpio'
pkgver=79
pkgrel=2
pkgdesc="A C and Python library for controlling GPIOs on a Raspberry Pi"
arch=('x86_64')
url="http://abyz.me.uk/rpi/pigpio/"
license=('custom:UNLICENSE')
groups=('yobuko' 'yobuko-mu-editor')
depends=('python')
makedepends=('make')
source=("https://github.com/joan2937/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('bae24b0a28b6865bf4e0903d9e1881344ab7cf26a513f295d178402a426f90f2fdd43444cfe899c0bc25939a129ebf47ecd1ac1600683cd648902d56825a3203')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -e 's/ -lrt//' -i Makefile
  sed -e 's/-Wl/\$(LDFLAGS)/' -i Makefile
  sed -e 's/\$(CC) -o/\$(CC) $(LDFLAGS) -o/' -i Makefile
  sed -e '/which python2/d' -i Makefile
  sed -e '/\/opt/d' -i Makefile
  sed -e 's|\$(prefix)/man|\$(prefix)/share/man|' -i Makefile
  sed -e 's|/usr/bin/pigpiod|/usr/bin/pigpiod -k|' -i util/pigpiod.service
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 util/pigpiod.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 UNLICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}