# Maintainer: Cthulu201 <cthulu201@duck.com>
# Contributor: lsf <lsf at pfho dot net>

pkgname=librewolf-bin
pkgver=115.0.2
pkgrel=2
pkgdesc="Community-maintained fork of Firefox, focused on privacy, security and freedom."
arch=(x86_64)
license=(MPL GPL LGPL)
groups=('yobuko')
url="https://librewolf-community.gitlab.io/"
provides=("${pkgname//-bin/""}")
conflicts=("${pkgname//-bin/""}")
replaces=("${pkgname//-bin/""}")
depends=('gtk3' 'libxt' 'startup-notification' 'mime-types' 'dbus-glib'
         'nss' 'ttf-font' 'libpulse' 'ffmpeg')
optdepends=('networkmanager:    Location detection via available WiFi networks'
            'libnotify:         Notification integration'
            'pulseaudio:        Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US:    Spell checking, American English')
validpgpkeys=('031F7104E932F7BD7416E7F6D2845E1305D6E801')
backup=('usr/lib/librewolf/librewolf.cfg'
        'usr/lib/librewolf/distribution/policies.json')
options=(!emptydirs)
install='librewolf-bin.install'

_project_id=44042130
source=("https://gitlab.com/api/v4/projects/${_project_id}/packages/generic/${pkgname//-bin/""}/${pkgver}-${pkgrel}/${pkgname//-bin/""}-${pkgver}-${pkgrel}-linux-x86_64-package.tar.bz2")
sha512sums=('d638b31681d37af996fbf8d44d18d5593ead91ab36aa1cdf23aebcf5287232c74ae5c1e7326752ecb013a9b947a879936c7c09643f2f43bbdd19c9c50498b2e1')

package() {
  cd "${srcdir}" || exit
  install -dm 755 ${pkgdir}/usr/lib/librewolf
  install -dm 755 ${pkgdir}/usr/bin
  cp -r "${srcdir}"/${pkgname//-bin/""}/* "${pkgdir}"/usr/lib/librewolf

  install -Dvm755 /dev/stdin "$pkgdir/usr/bin/${pkgname//-bin/""}" <<END
#!/bin/sh
exec /usr/lib/${pkgname//-bin/""}/librewolf "\$@"
END
}
