# Maintainer: Cthulu201 <cthulu201@duck.com>
# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Carlos Aznarán <caznaranl@uni.pe>
# Contributor: kastik <kastik69420@gmail.com>
# Contributor: Ismaël Bouya <ismael.bouya@normalesup.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=anaconda
pkgver=2023.03_1
pkgrel=1
pkgdesc="Package manager, an environment manager, a Python/R data science distribution"
arch=('x86_64')
url='https://www.anaconda.com/'
license=("BSD")
groups=('yobuko')
source=("https://repo.anaconda.com/archive/Anaconda3-${pkgver//_/-}-Linux-x86_64.sh")
depends=('libxau' 'libxi' 'libxss' 'libxtst' 'libxcursor' 'libxcomposite'
         'libxdamage' 'libxfixes' 'libxrandr' 'libxrender' 'mesa-libgl'
         'alsa-lib' 'libglvnd')
options=(!strip libtool staticlibs)
sha512sums=('69f69811ed962bb515c18bd2a802dcc5c909a340e69c6d744a9a41dcb45977030c3fdca08b298d55d12d1a88ca2a48a028757593013b4b70e9764625048a0bd3')

package() {
  cd "${srcdir}"
  install -dm0755 "${pkgdir}/usr/bin"
  
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so"
  bash ./"Anaconda3-${pkgver//_/-}-Linux-${CARCH}.sh" -b -p "${pkgdir}/opt/${pkgname}" -f
  [ "$BREAK_EARLY" = 1 ] && exit 1
  chmod a+r -R "${pkgdir}/opt/${pkgname}/pkgs"
  sed -e "s|${pkgdir}||g" -i $(grep "${pkgdir}" "${pkgdir}/opt/${pkgname}" -rIl 2>/dev/null)
#  echo 'apps = {}' >> "${pkgdir}/opt/${pkgname}/lib/python3.9/site-packages/anaconda_navigator/api/external_apps/__init__.py"
  install -D -m644 "${pkgdir}/opt/${pkgname}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  ln -s "/opt/${pkgname}/bin/${pkgname}-navigator" "${pkgdir}/usr/bin/${pkgname}-navigator"
  ln -s "/opt/${pkgname}/bin/conda" "${pkgdir}/usr/bin/conda"
}
