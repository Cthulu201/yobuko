# Maintainer: Cthulu201 <cthulu201@duck.com>

pkgname=thorium-browser
pkgver=122.0.6261.132
pkgrel=1
pkgdesc='Chromium fork named after radioactive element No. 90'
arch=(x86_64)
url=https://thorium.rocks/
license=(BSD3)
groups=('yobuko')
depends=('gtk3' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'libcups' 'libgcrypt'
         'ttf-liberation' 'dbus' 'libpulse' 'pciutils' 'libva' 'libffi'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('unzip')
options=(!strip)
source=("${pkgname}-${pkgver}.zip::https://github.com/Alex313031/${pkgname%-browser}/releases/download/M${pkgver}/${pkgname}_${pkgver}_amd64.zip"
        "https://raw.githubusercontent.com/Alex313031/thorium/main/LICENSE.md")
noextract=("${pkgname}-${pkgver}.zip")
sha512sums=('eda89d78643036dab5f7c1bdc01e46cae8c162d69c8837f1dcba67c6b0e443d414a3e09fd1d0b289c223c0a9ad1d46e447d04f491d2204739ef52ac81a564563'
            '5ce66769f90e532bf42deae2e6d7476934bddd3a201326217ede5d1fcad3569226b122f6a44f85cebd732b7b63cec6f0113d22e87442b07ac802f26067e5bbb8')

prepare() {
  unzip "${pkgname}-${pkgver}.zip" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" || exit

  install -dm0755 "${pkgdir}/usr/bin"
  install -dm0755 "${pkgdir}/opt/${pkgname}"
  install -Dm0644 "${srcdir}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"

  cp -a ./* "${pkgdir}/opt/${pkgname}/"
  ln -s "/opt/${pkgname}/${pkgname%-browser}" "${pkgdir}/usr/bin/${pkgname}"
}
